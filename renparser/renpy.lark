start: script

script: (default_statement | define_statement | label_statement | python_block_statement)*

default_statement: "default" /.+/ _NEWLINE
define_statement: "define" /.+/ _NEWLINE

label_statement: "label" NAME ":" _NEWLINE _INDENT label_block _DEDENT
label_block: label_logic_statement+
label_logic_statement: call_statement
                     | hide_statement
                     | if_statement
                     | inline_python_statement
                     | jump_statement
                     | menu_statement
                     | pause_statement
                     | pass_statement
                     | play_statement
                     | python_block_statement
                     | return_statement
                     | say_statement
                     | scene_statement
                     | show_statement
                     | stop_statement
                     | window_statement
                     | with_statement

pause_statement: "pause" FLOAT? _NEWLINE

// Audio
audio_fading: FADE FLOAT
audio_item: (NAME | STRING)
play_statement: "play" NAME audio_item audio_fading* _NEWLINE
stop_statement: "stop" NAME audio_fading* _NEWLINE

// Dialogue
say_statement: (say_statement_character | say_statement_narration | say_statement_string)
say_statement_character: NAME "@"? NAME* STRING _NEWLINE
say_statement_narration: STRING _NEWLINE
say_statement_string: STRING STRING _NEWLINE

// Flow control
call_statement: "call" NAME ["(" [/[^)]+/] ")"] _NEWLINE
else_statement: "else" ":" _NEWLINE _INDENT label_block _DEDENT
if_statement: "if" /[^:]+/ ":" _NEWLINE _INDENT label_block _DEDENT else_statement?
jump_statement: "jump" NAME _NEWLINE
pass_statement: "pass" _NEWLINE
return_statement: "return" _NEWLINE

// Menu
menu_statement: "menu" NAME? ":" _NEWLINE _INDENT menu_block _DEDENT
menu_block: say_statement? menu_choice+
menu_choice: STRING ":" _NEWLINE _INDENT menu_choice_block _DEDENT
menu_choice_block: label_logic_statement+

// Python
inline_python_statement: "$" /.+/ _NEWLINE
python_block_statement: "init"? INTEGER? "python" ["in" NAME] ":" _NEWLINE _INDENT python_block_line+ _DEDENT
python_block_line: /.+/ _NEWLINE [_INDENT python_block_line+ _DEDENT]

// Visual
hide_statement: "hide" NAME (_NEWLINE | with_statement)
scene_statement: "scene" NAME+ (_NEWLINE | with_statement)
show_statement: "show" NAME+ ["at" NAME+] (_NEWLINE | with_statement)
window_statement: "window" "auto"? ("hide" | "show") NAME? _NEWLINE
with_statement: "with" NAME _NEWLINE

COMMENT: /#.*\n/
FADE: ("fadein" | "fadeout")
NAME: /[_a-zA-Z0-9]+/

_NEWLINE: /(\n[ ]*)+/
DIGIT: "0".."9"
FLOAT: DIGIT+ "." DIGIT+
SPACE: " "
SPACES: SPACE+

%declare _INDENT _DEDENT
%import common.ESCAPED_STRING -> STRING
%ignore COMMENT
%ignore SPACES
